apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.google.cloud.tools.jib"

// 扩展定义
ext {
    // 启动类
    MainClass = 'com.changhong.sei.help.RestApplication'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // 本项目依赖模块
    compile project(':sei-help-api')
    // SEI平台依赖包
    compile("com.changhong.sei:sei-cloud-zookeeper-starter:$sei_version")
    compile("com.changhong.sei:sei-edm-sdk:$sei_version")
    // 第三方组件依赖包
    compile("mysql:mysql-connector-java:$mysqlVersion")
    compile("org.jsoup:jsoup:$jsoup_version")
    // markdown
    compile(
            "com.atlassian.commonmark:commonmark:$commonmark_version",
            "com.atlassian.commonmark:commonmark-ext-autolink:$commonmark_version",
            "com.atlassian.commonmark:commonmark-ext-gfm-tables:$commonmark_version"
    )
    // 测试依赖包
    // compile("com.changhong.sei:sei-test-starter:$sei_version")
    testCompile("com.changhong.sei:sei-test-starter:$sei_version")
}

// 默认application文件中的配置从配置中心获取
processResources {
    excludes = ["**/application.yaml", "**/application.properties"]
}

// 打ar包
bootJar  {
    enabled = true
    archiveName = 'sei-help.jar'
    mainClassName = MainClass
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

jar {
    enabled = true
    exclude('bootstrap.yaml')
    exclude('application.yaml')
    exclude('com/changhong/sei/help/RestApplication.*')
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

/////////////////////////////////////上传Maven仓库////////////////////////////////////////
// 打包源代码
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// 如果希望gradle install，安装到.m2本地仓库，参考下面的内容
install {
    repositories.mavenInstaller {
        pom.version = "$project.version"
        pom.artifactId = "$project.name"
        pom.groupId = "$project.group"
    }
}

//上传到nexus
uploadArchives {
    // 产品打jar包时需要开启
    enabled = true
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            pom.version = "$project.version"
            pom.artifactId = "$project.name"
            pom.groupId = "$project.group"
        }
    }
    return 'Success'
}

// 打镜像
jib {
    // 拉取的镜像的配置
    from {
        // oracle jdk
        image = 'rddgit.changhong.com:8083/alpine-java:latest'
        // open jdk
        //image = 'java:8-jdk-alpine'
    }
    // 要生成的镜像的配置
    to {
        image = "$docker_registry_url/$docker_registry_namespace/$rootProject.name"
        tags = ["${project.version}".toString(), 'latest']

        auth {
            username = "${docker_registry_username}"
            password = "${docker_registry_password}"
        }
    }
    container {
        // 容器创建时间用
        creationTime = 'USE_CURRENT_TIMESTAMP'
        // jvmFlgs: JVM 容器的参数，和 Dockerfile 的 ENTRYPOINT作用相同
        jvmFlags = [
                '-Djava.security.egd=file:/dev/./urandom',
                '-Dfile.encoding=utf-8',
                '-Duser.timezone=GMT+08'
        ]
        // jvmFlags = ['-Xms512m', '-Xdebug', '-Xmy:flag=jib-rules']
        // 启动类限定名
        mainClass = MainClass
        // 容器暴露的端口，和 Dockerfile 的EXPOSE作用相同
        ports = ['8080']
    }
    // 如果私有镜像仓库没有启用https，设置allowInsecureRegistries参数为true
    allowInsecureRegistries = true
}

